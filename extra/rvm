# RVM

# Double check that we have a good installation of RVM
if [[ ! (-d ~/.rvm && -s "$HOME/.rvm/scripts/rvm" && -x ~/.rvm/bin/rvm-prompt) ]]
then
  # No rvm, fake it.
  echo "WARNING: no rvm, faking it." # warn once, when sourcing this file
  ruby_version(){ print "%{$fg[magenta]%}system%{$reset_color%}"; }
  # Don't continue
  return
fi

source "$HOME/.rvm/scripts/rvm"

rvm_completion_dir=/Users/gabe/.rvm/scripts/zsh/Completion
# If == 0, then not in $fpath
if [[ $fpath[(I)$rvm_completion_dir] == 0 ]]
then
  # Add rvm ZSH completion script to $fpath so that compinit (autoload -U
  # compinit && compinit) will load the script.
  fpath+=(~/.rvm/scripts/zsh/Completion) # push onto fpath array
  autoload -U compinit && compinit
fi
unset rvm_completion_dir # don't litter

# RVM bash completion
# zsh comes with it baked in.
# For either one, use rvm[tab][tab], rvm use[tab][tab], etc.
# (No space, so NOT "rvm use [tab]")

ruby_version() {
  # rvm-prompt options:
  # i => interpreter ("ree", "ruby", "macruby")
  # v => version ("1.8.7", "0.6")
  # i + v => "ree-1.8.7" or "macruby-0.6"
  # g => gemset ("@rails3")
  # s => print "system" if using system intepreter (otherwise blank, for
  # 	   some reason)
  local version=$(~/.rvm/bin/rvm-prompt i v g s) # with gemset
  # If using global gemset, show nothing
  version=${version/@global/}

  print "%{$fg[magenta]%}${version}%{$reset_color%}"
}
